repositories {
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"
    defaultConfig {
        applicationId "org.kotlinacademy.android"
        minSdkVersion 18
        targetSdkVersion 27
        versionCode 13
        versionName "1.1.1"
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField "boolean", "BUILT_ON_TRAVIS", "${System.getenv('TRAVIS') ? true : false}"
    }
    buildTypes {
        debug {}
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            ext.betaDistributionReleaseNotesFilePath = "crashlytics_release_notes.txt"
            ext.betaDistributionGroupAliases = "android-testers"
        }
    }

    flavorDimensions "server"
    productFlavors {
        local {
            dimension "server"
        }
        prod {
            dimension "server"
        }
        mock {
            dimension "server"
        }
    }
}

dependencies {
    implementation project(':android:common')
    implementation dep.activitystarter_kotlin
    kapt dep.activitystarter_compiler
    implementation dep.firebase
    implementation dep.kotlinandroidviewbindings
    implementation dep.kotlin_preferences
    implementation dep.anko_coroutines

    implementation('com.crashlytics.sdk.android:crashlytics:2.9.2@aar') {
        transitive = true;
    }

    androidTestImplementation(dep.espresso, {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
    })
    androidTestImplementation(dep.espresso_intents, {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
    })
    androidTestImplementation(dep.espresso_contrib) {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
    }
}

androidExtensions {
    experimental = true
}

apply from: '../androidconfig.gradle'
apply plugin: 'com.google.gms.google-services'